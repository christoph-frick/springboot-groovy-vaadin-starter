buildscript {
    ext {
        gradleVaadinPluginVersion = '0.9.6'
        springBootVersion = '1.2.1.RELEASE'
    }
    repositories.jcenter()
    dependencies {
        classpath "fi.jasoft.plugin:gradle-vaadin-plugin:$gradleVaadinPluginVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'fi.jasoft.plugin.vaadin'
apply plugin: 'spring-boot'

repositories {
    jcenter()
    maven { url 'http://maven.vaadin.com/vaadin-addons' }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "app.Application"

jar {
    dependsOn vaadinCompileWidgetset, vaadinCompileThemes
    baseName = 'app'
    // version = '0.1.0'
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

bootRun {
	dependsOn vaadinCompileWidgetset, vaadinCompileThemes
}

vaadin {
	vaadin.version = '7.4.0.rc1'
	vaadin.debug = false
	vaadin.manageWidgetset = false
	vaadin.manageRepositories = false // fails with gradle 2.1 and 0.8 of the plugin
	widgetset jar.baseName
	vaadin.plugin.themesDirectory = 'build/resources/main/VAADIN/themes'
}

// some hoops to jump, to make spring-boot again dominant
bootRepackage.dependsOn = [jar]
assemble.dependsOn = [bootRepackage, jar]
project.convention.getPlugin(WarPluginConvention).webAppDirName = 'build/resources/main'

// pass sysprops down to bootRun, so we can add them in configs in intellij
bootRun.systemProperties = System.properties

project.afterEvaluate {
	jar {
		into('lib') {
			from configurations.vaadin
		}
	}
	bootRun {
		classpath += configurations.vaadin
	}
	// exclude spring here, due to the dep to javax validation 1.1
	configurations.'vaadin-client' {
		exclude module: 'spring-boot-starter-web'
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

project.codenarc {
	toolVersion = '0.22'
	configFile = new File('config/codenarc/ruleset.groovy')
}

dependencies {
    def vaadin4springVersion = '0.0.3.2'
    compile 'org.codehaus.groovy:groovy-all:2.4.0'
    compile 'org.springframework.boot:spring-boot-starter-web'
    // always exclude com.vaadin packages from deps
    compile("org.vaadin.spring:spring-boot-vaadin:${vaadin4springVersion}") { exclude group: 'com.vaadin' }
    compile('org.vaadin:viritin:1.25') { exclude group: 'com.vaadin' }
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    // vaadin addons are put automatically into the widgetset 
    // compile('org.vaadin.addons:ckeditor-wrapper-for-vaadin:7.9.3') { exclude group:'com.vaadin' }
}

