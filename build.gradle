plugins {
    id 'org.springframework.boot' version '1.5.3.RELEASE'
    id 'fi.jasoft.plugin.vaadin' version '1.1.11'
}

ext {
    ext {
        groovyVersion = '2.4.11'
        gradleWrapperVersion = '3.5'
        vaadinVersion = '8.0.6'
        vaadinSpringVersion = '2.0.1'
        viritinVersion = '1.61'
        codenarcVersion = '0.25.1'
        spockVersion = '1.0-groovy-2.4'
    }
    mainClassName = "app.Application" 
}

apply plugin: 'idea'
apply from: 'gradle/idea.gradle'
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'codenarc'

repositories {
    jcenter()
    maven { url 'https://maven.vaadin.com/vaadin-addons' }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileGroovy {
	groovyOptions.configurationScript = file("gradle/groovyOptions.groovy")
}

war {
    baseName = 'app'
    version = null // no version in the filename
    manifest {
        attributes 'Main-Class': mainClassName // Allow to run with `java -jar app.war`
    }
}

bootRun.systemProperties = System.properties // pass sysprops down to bootRun, so we can add them in configs in intellij

vaadin {
	vaadin.version = vaadinVersion
	vaadinCompile.widgetset 'app' // name of the widgetset (see @Widgetset in the UI)
	vaadinCompile.outputDirectory = file('build/resources/main/')
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

project.codenarc {
	toolVersion = codenarcVersion
	configFile = new File('config/codenarc/ruleset.groovy')
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-devtools'
    // always exclude com.vaadin packages from deps
    compile("com.vaadin:vaadin-spring-boot-starter:${vaadinSpringVersion}")
    compile("org.vaadin:viritin:${viritinVersion}") { exclude group: 'com.vaadin' }
    testCompile "org.spockframework:spock-core:${spockVersion}"
    // vaadin addons are put automatically into the widgetset 
    // compile('org.vaadin.addons:ckeditor-wrapper-for-vaadin:7.9.3') { exclude group:'com.vaadin' }
}
