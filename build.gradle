plugins {
    id 'org.springframework.boot' version '2.0.2.RELEASE'
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
    id 'groovy' // after spring, because it overrides to 2.4
    id 'com.devsoap.plugin.vaadin' version '1.3.1'
}

ext {
    ext {
        groovyVersion = '2.5.0'
        gradleWrapperVersion = '4.7'
        vaadinVersion = '8.4.2'
        vaadinSpringVersion = '3.0.0'
        spockVersion = '1.1-groovy-2.4'
    }
    mainClassName = "app.Application" 
}

repositories {
    jcenter()
    maven { url 'https://maven.vaadin.com/vaadin-addons' }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// turn on for static compilation of all Groovy files
// compileGroovy {
//     groovyOptions.configurationScript = file("gradle/groovyOptions.groovy")
// }

jar {
    baseName = 'app'
    version = null // no version in the filename
    manifest {
        attributes 'Main-Class': mainClassName // Allow to run with `java -jar app.jar`
    }
}

bootRun.systemProperties = System.properties // pass sysprops down to bootRun, so we can add them in configs in intellij

vaadin {
	vaadin.version = vaadinVersion
	vaadinCompile.widgetset 'app' // name of the widgetset (see @Widgetset in the UI)
	vaadinCompile.outputDirectory = file('build/resources/main/')
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
    distributionType = Wrapper.DistributionType.ALL
}

dependencies {
    compile("org.codehaus.groovy:groovy-all:${groovyVersion}")
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-devtools')
    // always exclude com.vaadin packages from deps
    compile("com.vaadin:vaadin-spring-boot-starter:${vaadinSpringVersion}")
    testCompile("org.spockframework:spock-core:${spockVersion}")
    // vaadin addons are put automatically into the widgetset 
    // compile('org.vaadin.addons:ckeditor-wrapper-for-vaadin:7.9.3') { exclude group:'com.vaadin' }
}
